\documentclass{article}
\usepackage{fullpage}
\usepackage[all]{xy}
\begin{document}
\section{Introduction}

If people say that Racket is just a Scheme, they are short-selling Racket a little. It's more accurate to say that Racket is a language laboratory, with support for many different languages.

Is that really true? Racket does include a nice macro system, which allows a programmer to add in new language constructs. For example, we can get while loops into Racket with relative ease:
\begin{verbatim}
#lang racket
(define-syntax-rule (while test body ...)
  (local [(define (loop)
            (when test
              body ...
              (loop)))]
    (loop)))
;; From this point forward, we've got while loops.
(while (not (string=? (read-line) "quit"))
  (printf "never going to give you up\n")
  (printf "never going to let you down\n"))
\end{verbatim}
So we can certainly extend the language. But this still looks just like a Scheme.

Let's take a closer look at a Racket program. Every Racket program begins with a funny line at the very top that, on first glance, looks redundant:
\begin{verbatim}
#lang racket
\end{verbatim}
Why in the world does a Racket program need to say that it's a Racket program? Isn't that obvious?


We can understand the situation better by looking at another environment on our desktop, namely the web browser. A web browser supports different kinds of HTML variants, since HTML is a moving target, and browsers have come up with crazy rules for figuring out how to take an arbitrary document and decide what HTML parsing rules to apply to it.

HTML 5 tries to make this determination somewhat more straightforward: we can define an HTML 5 document by putting a DOCTYPE element at the very top of the file which self-describes the document as being html.
\begin{verbatim}
<!DOCTYPE html>
<html lang="en">
  <head><title>Hello world</title></head>
  <body><p>Hello world!</p></body>
</html>
\end{verbatim}

Going back to the world of Racket, we see by analogy that the \verb+#lang+ line in a Racket program is a self-description of how to treat the rest of the program. (Actually, the \verb+#lang+ line is quite bit more active than this, but we'll get to this in a moment.)

The racket part in the \verb+#lang+ line isn't inevitable: the main Racket distribution, in fact, comes bundled with several languages which can take the place of the word racket. Many of these languages (racket/base, typed/racket, lazy) still look like Racket... but some of them don't. Here's one example:
\begin{verbatim}
#lang datalog
ancestor(A, B) :- parent(A, B).
ancestor(A, B) :-
  parent(A, C), D = C, ancestor(D, B).
parent(john, douglas).
parent(bob, john).
ancestor(A, B)?
\end{verbatim}
This is an example of a Datalog program that deals with logical relations. Neat!


What might be surprising is that the mechanism for using different languages in Racket is wide open. Let's expand our minds.
\begin{verbatim}
#lang planet dyoo/bf
++++++[>++++++++++++<-]>.
>++++++++++[>++++++++++<-]>+.
+++++++..+++.>++++[>+++++++++++<-]>.
<+++[>----<-]>.<<<<<+++[>+++++<-]>.
>>.+++.------.--------.>>+.
\end{verbatim}
This language does not look like Racket. It looks like line noise. This is brainf*ck. Although this language is not included in the main distribution, because it is on PLaneT, anyone with Racket can easily play with it.


Ignoring the question of why?!! someone would do this, let's ask another: how do we build this? This tutorial will cover how to build this language into Racket from scratch.

Let's get started!

\section{The view from high orbit}
We want to teach Racket what it means when we say something like:
\begin{verbatim}
#lang planet dyoo/bf
,[.,]
\end{verbatim}
As mentioned earlier, a \verb+#lang+ line is quite active: it tells the Racket runtime how to convert from the surface syntax to a meaningful program. Programs in Racket get digested in a few stages; the process looks something like this:
%
\begin{displaymath}
\xymatrix {
    \txt{surface syntax} \ar[rr]^-{\txt{reader}}  & &\txt{AST} \ar[rr]^-{\txt{macro\\expansion}} & & \txt{core forms}}
\end{displaymath}
%
When Racket sees \verb+#lang planet dyoo/bf+, it will look for a particular module that we call a reader; a reader consumes surface syntax and excretes ASTs, and these ASTs are then annotated so that Racket knows how to make sense out of them later on. At this point, the rest of the Racket infrastructure kicks in and macro-expands the ASTs out, ultimately, to a core language.

So here's what we'll do:
\begin{itemize}
\item Capture the meaning of brainf*ck by writing a semantics module.

\item Go from the line noise of the surface syntax into a more structured form by writing a parser module.

\item Connect the pieces, the semantics and the surface syntax parser, by making a reader module.

\item Profit!
\end{itemize}


\section{Flight preparations}
Since we're starting from scratch, let's first make a work directory where we'll keep our source code. I'll call the directory ``bf/'', but you can use whatever name you want.
\begin{verbatim}
$ mkdir bf
\end{verbatim}
Ultimately, we want to put the fruit of our labor onto PLaneT, since that'll make it easier for others to use our work. Let's set up a PLaneT development link so the Racket environment knows about our work directory. I already have an account on PLaneT with my username dyoo. You can get an account fairly easily.

If we enter the following at the command line,
\begin{verbatim}
$ planet link dyoo bf.plt 1 0 bf
\end{verbatim}
we'll make a development link that will associate any module path of the form (planet dyoo/bf/...) to our local ``bf/'' directory. Later on, when we create a package and upload it to PLaneT, we can drop this development link, and then all the references that use (planet dyoo/bf/...) will immediately switch over to the one on the PLaneT server.


But does the link actually work? Let's write a very simple module in our work directory, and then see that Racket can find it through PLaneT.
\begin{verbatim}
$ cd bf
~/bf$ cat >hello.rkt
#lang racket
"hello world"
\end{verbatim}
Ok, let's see if Racket can find our magnificent ``hello.rkt'' module if we use the PLaneTized version of the name.
\begin{verbatim}
~/bf$ racket
Welcome to Racket v5.2.
> (require (planet dyoo/bf/hello))
"hello world"
>
\end{verbatim}
If we get to this point, then we've got the PLaneT development link in place.


\section{The brainf*ck language}
When we look at the definition of brainf*ck, it's actually not too bad. There's two bits of state,

\begin{itemize}
\item a byte array of data, and
\item a pointer into that data array
\end{itemize}
%
and it has only a few operations that affect this state:
\begin{itemize}
\item Increment the data pointer (\verb|>|)

\item Decrement the data pointer (\verb|<|)

\item Increment the byte at the data pointer (\verb|+|)

\item Decrement the byte at the data pointer (\verb|-|)

\item Write a byte to standard output (\verb|.|)

\item Read a byte from standard input (\verb|,|)

\item Perform a loop until the byte at the data pointer is zero (\verb|[|, \verb|]|)
\end{itemize}

Let's write a module that lets us play with such a system: let's call it ``semantics.rkt''.
\begin{verbatim}
#lang racket
 
(require rackunit)                ;; for unit testing
(provide (all-defined-out))
 
;; Our state contains two pieces.
(define-struct state (data ptr)
  #:mutable)
 
;; Creates a new state, with a byte array of 30000 zeros, and
;; the pointer at index 0.
(define (new-state)
  (make-state (make-vector 30000 0) 0))
 
;; increment the data pointer
(define (increment-ptr a-state)
  (set-state-ptr! a-state (add1 (state-ptr a-state))))
 
;; decrement the data pointer
(define (decrement-ptr a-state)
  (set-state-ptr! a-state (sub1 (state-ptr a-state))))
 
;; increment the byte at the data pointer
(define (increment-byte a-state)
  (define v (state-data a-state))
  (define i (state-ptr a-state))
  (vector-set! v i (add1 (vector-ref v i))))
 
;; decrement the byte at the data pointer
(define (decrement-byte a-state)
  (define v (state-data a-state))
  (define i (state-ptr a-state))
  (vector-set! v i (sub1 (vector-ref v i))))
 
;; print the byte at the data pointer
(define (write-byte-to-stdout a-state)
  (define v (state-data a-state))
  (define i (state-ptr a-state))
  (write-byte (vector-ref v i) (current-output-port)))
 
;; read a byte from stdin into the data pointer
(define (read-byte-from-stdin a-state)
  (define v (state-data a-state))
  (define i (state-ptr a-state))
  (vector-set! v i (read-byte (current-input-port))))
 
;; loops
(define-syntax-rule (loop a-state body ...)
  (local [(define (loop)
            (unless (= (vector-ref (state-data a-state) (state-ptr a-state))
                       0)
              body ...
              (loop)))]
    (loop)))
\end{verbatim}
Ok, that doesn't seem too bad. But of course, we should test this; let's use the \verb|rackunit| unit testing framework and tickle this code. Let's add a little more to the end of ``semantics.rkt''.
\begin{verbatim}
"semantics.rkt"
;; Simple exercises.
(let ([s (new-state)])
  (increment-byte s)
  (check-equal? 1 (vector-ref (state-data s) 0))
  (increment-byte s)
  (check-equal? 2 (vector-ref (state-data s) 0))
  (decrement-byte s)
  (check-equal? 1 (vector-ref (state-data s) 0)))
 
;; pointer movement
(let ([s (new-state)])
  (increment-ptr s)
  (increment-byte s)
  (check-equal? 0 (vector-ref (state-data s) 0))
  (check-equal? 1 (vector-ref (state-data s) 1))
  (decrement-ptr s)
  (increment-byte s)
  (check-equal? 1 (vector-ref (state-data s) 0))
  (check-equal? 1 (vector-ref (state-data s) 1)))
 
;; make sure standard input is doing something
(let ([s (new-state)])
  (parameterize ([current-input-port
                  (open-input-bytes (bytes 3 1 4))])
    (read-byte-from-stdin s)
    (increment-ptr s)
    (read-byte-from-stdin s)
    (increment-ptr s)
    (read-byte-from-stdin s))
  (check-equal? 3 (vector-ref (state-data s) 0))
  (check-equal? 1 (vector-ref (state-data s) 1))
  (check-equal? 4 (vector-ref (state-data s) 2)))
 
 
;; make sure standard output is doing something
(let ([s (new-state)])
  (set-state-data! s (vector 80 76 84))
  (let ([simulated-stdout (open-output-string)])
    (parameterize ([current-output-port simulated-stdout])
      (write-byte-to-stdout s)
      (increment-ptr s)
      (write-byte-to-stdout s)
      (increment-ptr s)
      (write-byte-to-stdout s))
    (check-equal? "PLT" (get-output-string simulated-stdout))))
 
 
;; Let's see that we can clear.
(let ([s (new-state)])
  (set-state-data! s (vector 0 104 101 108 112 109 101 105
                            109 109 101 108 116 105 110 103))
  (set-state-ptr! s 15)
  ;; [ [-] < ]
  (loop s
        (loop s (decrement-byte s))
        (decrement-ptr s))
 
  (check-equal? 0 (state-ptr s))
  (check-equal? (make-vector 16 0) (state-data s)))
\end{verbatim}
Good! Our tests, at the very least, let us know that our definitions are doing something reasonable, and they should all pass.

However, there are a few things that we may want to fix in the future, like the lack of error trapping if the input stream contains eof. And there's no bounds-checking on the ptr or on the values in the data. Wow, there are quite a few things that we might want to fix. But at the very least, we now have a module that captures the semantics of brainf*ck.

\section{Lisping a language}
We might even be cheeky enough to insist that people write brainf*ck programs with s-expressions. Let's take that route, and create a module language that uses our ``semantics.rkt''. We'll create such a module language in ``language.rkt''.
\begin{verbatim}
"language.rkt"
#lang racket
 
(require "semantics.rkt")
 
(provide greater-than
         less-than
         plus
         minus
         period
         comma
         brackets
         (rename-out [my-module-begin #%module-begin]))
 
;; The current-state is a parameter used by the
;; rest of this language.
(define current-state (make-parameter (new-state)))
 
;; Every module in this language will make sure that it
;; uses a fresh state.
(define-syntax-rule (my-module-begin body ...)
  (#%plain-module-begin
    (parameterize ([current-state (new-state)])
       body ...)))
 
(define-syntax-rule (greater-than)
  (increment-ptr (current-state)))
 
(define-syntax-rule (less-than)
  (decrement-ptr (current-state)))
 
(define-syntax-rule (plus)
  (increment-byte (current-state)))
 
(define-syntax-rule (minus)
  (decrement-byte (current-state)))
 
(define-syntax-rule (period)
  (write-byte-to-stdout (current-state)))
 
(define-syntax-rule (comma)
  (read-byte-from-stdin (current-state)))
 
(define-syntax-rule (brackets body ...)
  (loop (current-state) body ...))
\end{verbatim}
This ``language.rkt'' presents brainf*ck as a s-expression-based language. It uses the semantics we've coded up, and defines rules for handling greater-than, less-than, etc... We have a parameter called current-state that holds the state of the brainf*ck machine that's used through the language.

There's one piece of this language that looks particularly mysterious: what's the \verb+#%module-begin+ form, and what is it doing? In Racket, every module has an implicit \verb+#%module-begin+ that wraps around the entirety of the module's body. We can see this by asking Racket to show us the results of the expansion process; here's a small example to demonstrate.
\begin{verbatim}
> (syntax->datum
   (expand '(module an-example-module '#%kernel
              "hello"
              "world")))
'(module an-example-module '#%kernel (#%module-begin '"hello" '"world"))
\end{verbatim}
Ignore, for the moment, the use of syntax->datum or the funky use of \verb+'#%kernel+. What we should notice is that Racket has added in that \verb+#%module-begin+ around the \verb+"hello"+ and \verb+"world"+. So there's the implicit wrapping that Racket is doing.


It turns out that \verb+#%module-begin+ can be really useful! In particular, we want to guarantee that every module written in brainf*ck runs under a fresh state. If we had two brainf*ck programs running, say like this:
\begin{verbatim}
(require "my-first-bf-program.rkt")
(require "my-second-bf-program.rkt")
\end{verbatim}
then it would be a shame to have the two programs clash just because they brainf*cked each other's data! By defining our own \verb+#%module-begin+, we can ensure that each brainf*ck module has its own fresh version of the state, and our definition of my-module-begin does this for us.


Once we've written ``language.rkt'', we can use the language like this:
\begin{verbatim}
#lang s-exp (planet dyoo/bf/language)
 
(plus)(plus)(plus)(plus)(plus) (plus)(plus)(plus)(plus)(plus)
(brackets
 (greater-than) (plus)(plus)(plus)(plus)(plus) (plus)(plus)
 (greater-than) (plus)(plus)(plus)(plus)(plus) (plus)(plus)
 (plus)(plus)(plus) (greater-than) (plus)(plus)(plus)
 (greater-than) (plus) (less-than)(less-than)(less-than)
 (less-than) (minus))
(greater-than) (plus)(plus) (period)
(greater-than) (plus) (period)
(plus)(plus)(plus)(plus)(plus) (plus)(plus) (period)
(period) (plus)(plus)(plus) (period)
(greater-than) (plus)(plus) (period)
(less-than)(less-than) (plus)(plus)(plus)(plus)(plus)
(plus)(plus)(plus)(plus)(plus) (plus)(plus)(plus)(plus)(plus)
(period) (greater-than) (period)
(plus)(plus)(plus) (period)
(minus)(minus)(minus)(minus)(minus)(minus)(period)
(minus)(minus)(minus)(minus)(minus)(minus)(minus)(minus)
(period)(greater-than) (plus) (period) (greater-than) (period)
\end{verbatim}
The \verb+#lang+ line here is saying, essentially, that the following program is written with s-expressions, and should be treated with the module language ``language.rkt'' that we just wrote up. And if we run this program, we should see a familiar greeting. Hurrah!

... But wait! We can't just declare victory here. We really do want to allow the throngs of brainf*ck programmers to write brainf*ck in the surface syntax that they deserve. Keep ``language.rkt'' on hand, though. We will reuse it by having our parser transform the surface syntax into the forms we defined in ``language.rkt''.

Let's get that parser working!

\section{Parsing the surface syntax}

The Racket toolchain includes a professional-strength lexer and parser
in the parser-tools collection.  For the sake of keeping this example
terse, though, we'll write a simple recursive-descent parser without
using the parser-tools collection.

The expected output of a successful parse should be some kind of
abstract syntax tree. What representation should we use for the tree?
For the purposes of this tutorial, let's use plain s-expressions as
our representation.

(As an alternative to plain s-expressions, we could have use an
alternative data structure in Racket called a syntax object, which
allows us to add additional attributes like source location to each
datum.  For the case of brainf*ck, we might not care, but if we were
to write a parser for a more professional, sophisticated language,
such as LOLCODE, then we'd want source locations so we can give good
error messages during parsing or run-time.)


Let's write a parser. Our parser will consume an input-port, from
which we can read in bytes with
\verb|read-byte|.  We'll write the following into ``parser.rkt''.
\begin{verbatim}
"parser.rkt"
#lang racket

;; The only visible export of this module will be parse-expr.
(provide parse-expr)


(define (parse-expr inp)
 (define ch (read-char inp))
 (cond
   [(eof-object? ch)
    eof]
   [else
    (case ch
      [(#\.) '(dot)]
      [(#\,) '(comma)]
      [(#\+) '(plus)]
      [(#\-) '(minus)]
      [(#\<) '(less-than)]
      [(#\>) '(greater-than)]
      [(#\[) `(brackets ,@(parse-exprs inp))]
      [else
       (parse-expr inp)])]))

(define (parse-exprs inp)
 (define peeked (peek-char inp))
 (cond
   [(eof-object? peeked)
    (error 'parse "Expected ], but eof received instead")]
   [(member peeked '(#\. #\, #\+ #\- #\< #\> #\[))
    (cons (parse-expr inp) (parse-exprs inp))]
   [(char=? peeked #\])
    (read-char inp)
    empty]
   [else
    (read-char inp)
    (parse-exprs inp)]))

\end{verbatim}

This parser isn't anything too tricky, although there's a little bit
of messiness because it needs to handle brackets recursively. That
part is supposed to be a little messy anyway, since it's the capstone
that builds tree structure out of a linear character stream.  (If we
were parsing a fully parenthesized language, we could simply use the
built-in \verb|read-syntax| function in Racket, which already knows
how to parse parenthesized structure.)


Let's see if this parser does anything useful:
\begin{verbatim}
> (define my-sample-input-port (open-input-string ",[.,]"))
> (define first-ast
    (parse-expr my-sample-input-port))
> first-ast
'(comma)
> (define second-ast
    (parse-expr my-sample-input-port))
> second-ast
'(brackets (period) (comma))
> (parse-expr my-sample-input-port)
#<eof>
\end{verbatim}
Good! So we're able to parse s-expressions out of an input stream.

We should note that the parser is generating syntax objects that use
the same names as the defined names we have in our ``language.rkt''
module language. Yup, that's deliberate, and we'll see why in the next
section.


We mentioned that the parser wasn't too hard... but then again, we
haven't written particularly good traps for error conditions.  The
error messages, furthermore, do not tell Racket's runtime system at
what position a syntactic error message can occur.  In short: this
parser is a baby parser. If we were more rigorous, we'd probably
implement it with the parser-tools collection, write unit tests for
the parser with rackunit, and make sure to produce good error messages
when Bad Things happen (like having unbalanced brackets or
parentheses.

Still, we've now got the language and a parser. How do we tie them together?

\section{Crossing the wires}
This part is fairly straightforward. We have two pieces in hand:

\begin{itemize}
\item A parser in ``parser.rkt'' for the surface syntax that produces ASTs

\item A module language in ``language.rkt'' that provides the meaning for those ASTs.
\end{itemize}

To combine these two pieces together, we want to define a reader that
associates the two. When Racket encounters a \verb+#lang+ line of the
form:
\begin{verbatim}
#lang planet dyoo/bf
\end{verbatim}
it will look for a reader module in ``lang/reader.rkt'' and use it to
parse the file.  Racket provides a helper module called
\verb|syntax/module-reader| to handle most of the dirty work; let's use
it. Make a ``lang/'' subdirectory, and create ``reader.rkt'' in that
subdirectory, with the following content:
\begin{verbatim}
"lang/reader.rkt"
#lang s-exp syntax/module-reader
(planet dyoo/bf/language)
#:read my-read
#:read-syntax my-read-syntax
 
(require "../parser.rkt")
 
(define (my-read in)
  (parse-expr src in))
 
(define (my-read-syntax src in)
  (datum->syntax #f (parse-expr src in)))
\end{verbatim}
Some of this is magic, so let's step through this. The second line of
the file tells \verb|syntax/module-reader| that any abstract syntax trees
that come out are intended to take on their semantics from our
language module ``language.rkt''. \verb|syntax/module-reader| is predisposed to
assume that the individual expressions of our programs are read using read and read-syntax, so we
override that default and plug in our \verb|parse-expr| function into place.
Now that we have all these pieces together, does any of this work?
Let's try it!
\begin{verbatim}
$ cat hello2.rkt
#lang planet dyoo/bf
++++++[>++++++++++++<-]>.
>++++++++++[>++++++++++<-]>+.
+++++++..+++.>++++[>+++++++++++<-]>.
<+++[>----<-]>.<<<<<+++[>+++++<-]>.
>>.+++.------.--------.>>+.
 
$ racket hello2.rkt
Hello, World!
\end{verbatim}
Sweet, sweet words.

\section{Landing on PLaneT}
Finally, we want to get this work onto PLaneT so that other people can share in the joy of writing brainf*ck in Racket. Let's do it!

First, let's go back to the parent of our work directory. Once we're there, we'll use the planet create command.
\begin{verbatim}
$ planet create bf
planet create bf
MzTarring ./...
MzTarring ./lang...
 
WARNING:
        Package has no info.rkt file. This means it will not have a description
        or documentation on the PLaneT web site.
 
$ ls -l bf.plt
-rw-rw-r-- 1 dyoo nogroup 3358 Jun 12 19:39 bf.plt
\end{verbatim}

There are a few warnings, because we haven't defined an ``info.rkt'' which provides metadata about our package. Good, diligent citizens would write an ``info.rkt'' file, so let's write one.
\begin{verbatim}
"info.rkt"
#lang setup/infotab
(define name "bf: a brainf*ck compiler for Racket")
(define categories '(devtools))
(define can-be-loaded-with 'all)
(define required-core-version "5.1.1")
(define version "1.0")
(define repositories '("4.x"))
(define scribblings '())
(define primary-file "language.rkt")
(define blurb
  '("Provides support for the brainf*ck language."))
(define release-notes
  '((p "First release")))
\end{verbatim}
Before we upload the package, let's make sure the ``bf.plt'' package works for us locally. We'll simulate an installation. First, let's break the development link.
\begin{verbatim}
$ planet unlink dyoo bf.plt 1 0
\end{verbatim}
If we try running our test program from before, it should fail on us.
\begin{verbatim}
$ racket hello2.rkt
require: PLaneT could not find the requested package: Server had no matching package:
         No package matched the specified criteria
\end{verbatim}
Ok, that was expected. Since we've dissolved the development link, and since we haven't uploaded the package onto the PLaneT network yet, we see the error that we expect to see.
Next, let's use planet fileinject to simulate an installation of our package from PLaneT.
\begin{verbatim}
$ planet fileinject dyoo bf.plt 1 0
planet fileinject dyoo bf.plt 1 0
 
============= Installing bf.plt on Sun, 12 Jun 2011 19:49:50 =============
raco setup: Unpacking archive from /home/dyoo/bf.plt
...
\end{verbatim}
Lots and lots of output later, the package should be installed.


If we try running our test program again...
\begin{verbatim}
$ racket hello2.rkt
Hello, World!
\end{verbatim}
Good! This simulates the situation where the package has been installed from PLaneT.

Once we're finally satisfied with the package's contents, we can finally upload it onto PLaneT. If you log onto planet.racket-lang.org, the user interface will allow you to upload your ``bf.plt'' package.


\end{document}
